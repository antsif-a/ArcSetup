buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    ext {
        arcVersion = 'v126.2'

        arcModule = { String name ->
            if(name.contains(':')) name = name.split(':')[name.split(':').length - 1]
            return "com.github.Anuken.Arc:$name:$arcVersion"
        }
    }

    dependencies {
        classpath arcModule('arc-core')
        classpath arcModule('extensions:packer')
    }
}

allprojects {
    apply plugin: 'java'

    version = '1.0'
    sourceCompatibility = 1.8

    ext {
        appName = 'ArcSetup'
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

project(":desktop") {
    dependencies {
        implementation project(":core")
        implementation arcModule('backends:backend-sdl')
        implementation arcModule('natives:natives-desktop')
        implementation arcModule('natives:natives-freetype-desktop')
    }
}

project(":core") {
    apply plugin: 'java-library'

    dependencies {
        api arcModule('arc-core')
        api arcModule('extensions:freetype')
    }
}
